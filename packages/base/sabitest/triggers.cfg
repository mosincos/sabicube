// triggers.cfg

keytrigger = [
	if (= (getactivecrosshair) 0) [
		echo "Nothing to see here"
	]
	if (= (getactivecrosshair) 1) [
		getitem
	]
]
////////////////////////////////////////////////////////////////////////////////
level_trigger_2 = [
	echo "This door is locked"
]
item_activate_102 = [
	showgui hack
]
rave = 0
item_activate_177 = [
	if (= $rave 0) [
		rave = 1
		raveon
	] [
		rave = 0
		raveoff
	]
]
item_activate_381 = [
	if (= (inventoryread 61) 0) [
		showgui needbait
	] [
		// reference to fishing.cfg
		showgui fishing
	]
]

item_activate_178 = [
	showgui npcinfo
]
newgui npcinfo [
	guilist [
		guilist [
			guitext "These are NPCs"
			guibar
			guitext "They are not following any particular path" 0
			guitext "they walk a straight line and turn around if they hit something" 0
			guitext "if you approach, they will greet you" 0
			guitext "if you hit/shoot, theyll get mad and come after you" 0
			guitext "if a NPC see you harming another, theyll also get mad" 0
			guitext "fortunatly they have a very kind nature" 0
			guitext "and will forgive everything, once you leave and come back" 0
			guitext "" 0
			guibutton "Close" [
				cleargui
			]
		]
	]
] "SabiCube NPC-test"

item_activate_176 = [
	showgui gardeninfo
]
newgui gardeninfo [
	guilist [
		guilist [
			guitext "This is a garden"
			guibar
			guitext "when your crosshair turns green, click E to pick up" 0
			guitext "click TAB to access your inventory" 0
			guitext "plants/shrooms have different properties" 0
			guitext "some will heal you, others will kill you" 0
			guitext "click their name to consume them " 0
			guitext "click their image to set hotkey 0-9" 0
			guitext "" 0
			guibutton "Close" [
				cleargui
			]
		]
	]
] "Sabi-garden"
////////////////////////////////////////////////////////////////////////////////
item_activate_341 = [
	if (= (inventoryread 9) 0) [
		showgui needshovel
	] [
		showgui haveshovel
	]
]
newgui needshovel [
	guilist [
		guilist [
			guitext "You must find a shovel" 0
			guitext "before you can dig" 0
			guimodelpreview (mapmodelname 9) "inventory" [cleargui] 4 1
		]
	]
] "Digging"
newgui haveshovel [
	guitext "This grave is empty" 0
	guibar
	guibutton "Damn..." [cleargui] 0
] "Digging"
////////////////////////////////////////////////////////////////////////////////

// ARENA

doorstate = 0
level_trigger_1 = [
	echo "This door is controlled by a lever"
]
item_activate_149 = [ // door switch (arena-enter)
	if (= ($doorstate) 0) [
		sound 76
		trigger 1 1
		doorstate = 1
	] [
		sound 76
		trigger 1 0
		doorstate = 0
	]
]
celldoor1 = 0
item_activate_120 = [ // celldoor1 switch
	if (= ($celldoor1) 0) [
		platform 1 1
		celldoor1 = 1
	] [
		platform 1 0
		celldoor1 = 0
	]
]

////////////////////// lockpicking chest
chest01state = 1
alias lockpicking [
	x = (rnd 5)
	if (= ($x) 0) [
		inventorywrite 71 -1 0
		sound 95
		echo "You broke the lockpick"
	]
	if (> ($x) 0) [
		sound 94
	]
	if (= ($x) 1) [
		chest01state = 0
		trigger 3 1
		showgui lootchest01
	]
]
item_activate_397 = [
	if (= ($chest01state) 1) [ // is the chest empty
		if (= (getlockpicking) 1) [ // do player have enough lockpicking skill
			if (>= (inventoryread 71) 1) [ // do player have at least one lockpick
				lockpicking
			] [
				echo "You need a lockpick to open this chest"
			]
		] [
			echo "You need better skills to open this chest"
		]
	] [
		echo "this chest is empty"
	]
]
newgui lootchest01 [
	guistayopen [
		guitext "25 silver coins" 0
		guitext "" 0
		guibutton "Take all" [cleargui] 0
	]
] 0

// a simple "hacking" script

digit_1 = 1
digit_1_set = 0
digit_2 = 2
digit_2_set = 0
digit_3 = 3
digit_3_set = 0

alias hackerror [
	digit_1_set = 0
	digit_2_set = 0
	digit_3_set = 0
	sound 74
	cleargui
]

newgui hack [
	guistayopen [
		guitext "Please enter your" 0
		guitext "four digit pincode" 0
		guibar
		guilist [
			guibutton "" [ hackerror ] "7"
			guibutton "" [ hackerror ] "8"
			guibutton "" [ hackerror ] "9"
		]
		guilist [
			guibutton "" [
				if (= $digit_3_set 1) [
					sound 75
					hackcomplete = 1
					echo "access granted"
					trigger 2 1
					sound 76
					cleargui
				] [	hackerror ]
			] "4"
			guibutton "" [ hackerror ] "5"
			guibutton "" [ hackerror ] "6"
		]
		guilist [
			guibutton "" [
				if (= $digit_1_set 0) [
					digit_1_set = 1
					sound 75
				] [	hackerror ]
			] "1"
			guibutton "" [
				if ((= $digit_1_set 1) && (= $digit_2_set 0)) [
					digit_2_set = 1
					sound 75
				] [	hackerror ]
			] "2"
			guibutton "" [
				if ((= $digit_2_set 1) && (= $digit_3_set 0)) [
					digit_3_set = 1
					sound 75
				] [	hackerror ]
			] "3"
		]
		guilist [
			guibutton "" [echo "Nothing here"] "64x64"
			guibutton "" [ hackerror ] "\0"
			guibutton "" [echo "Nothing here"] "64x64"
		]
		guibar
		guibutton "Abort" [
			digit_1_set = 0
			digit_2_set = 0
			digit_3_set = 0
			cleargui
		]
	]
]

// rave-script by Zoocata //
////////////////////////////
spam = 0
alias raveon [
	if (= $spam 0) [
		echo "Rave time!"
		fog 55
		spam = 1
	]
	//
	sleep 100 [
		fogcolour (rnd 255) (rnd 255) (rnd 255)
		watercolour (rnd 255) (rnd 255) (rnd 255)
		cloudcolour (rnd 255) (rnd 255) (rnd 255)
		cloudboxcolour (rnd 255) (rnd 255) (rnd 255)
		skyboxcolour (rnd 255) (rnd 255) (rnd 255)
		cloudlayer textures/mitaman/mm-auggiedog
		cloudscrollx (rnd 5)
		cloudscrolly (rnd 5)
//		cloudheight 100
//		cloudscale .25
		skybox skyboxes/mayhem/eclipse
		spinsky (rnd 25)
//		spam = 0
//		mapsound "rave" 100 1 // sound 0 UP
	]
]

alias raveoff [
	echo "Party's over!!"
	clearsleep
	fog 1000024
	skyboxcolour 255 255 255
	cloudboxcolour 255 255 255
	watercolour ""
	cloudcolour 255 255 255
	cloudscrollx 0
	cloudscrolly 0
	cloudlayer ""
//	cloudheight 0
//	cloudscale 0
	spinsky 0
	skybox skyboxes/humus/meadow
	spam = 0
//	mapsound "rave" 1 1 // sound 0 DOWN
]
////////////////////////////

item_activate_179 = [ // alchemist
	showgui alchemistsayhi
]