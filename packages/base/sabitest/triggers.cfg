// triggers.cfg

keytrigger = [
	if (= (getactivecrosshair) 0) [
		echo "Nothing to see here"
	]
	if (= (getactivecrosshair) 1) [
		getitem
	]
]

////////////////////////////////////////////////////////////////////////////////
level_trigger_2 = [
	echo "This door is locked"
]
item_activate_102 = [
	showgui hack
]

item_activate_381 = [
	if (= (inventoryread 61) 0) [
		showgui needbait
	] [
		// reference to fishing.cfg
		showgui fishing
	]
]

////////////////////////////////////////////////////////////////////////////////
item_activate_341 = [
	if (= (inventoryread 9) 0) [
		showgui needshovel
	] [
		showgui haveshovel
	]
]
newgui needshovel [
	guitext "You must find a shovel" 0
	guitext "before you can dig" 0
	guimodelpreview (mapmodelname 9) "inventory" [cleargui] 4 1
] "Digging"
newgui haveshovel [
	guitext "This grave is empty" 0
	guibar
	guibutton "Damn..." [cleargui] 0
] "Digging"
////////////////////////////////////////////////////////////////////////////////

doorstate = 0
level_trigger_1 = [
	echo "This door is controlled by a lever"
]
item_activate_149 = [ // door switch
	if (= ($doorstate) 0) [
		sound 76
		trigger 1 1
		doorstate = 1
	] [
		sound 76
		trigger 1 0
		doorstate = 0
	]
]

////////////////////// lockpicking chest
chest01state = 1
lockpickskill = 1
alias lockpicking [
	x = (rnd 5)
	if (= ($x) 0) [
		inventorywrite 71 -1 0
		sound 95
		echo "You broke the lockpick"
	]
	if (> ($x) 0) [
		sound 94
	]
	if (= ($x) 1) [
		chest01state = 0
		trigger 3 1
		showgui lootchest01
	]
]
item_activate_397 = [
	if (= ($chest01state) 1) [ // is the chest empty
		if (= ($lockpickskill) 1) [ // do player have enough lockpicking skill
			if (>= (inventoryread 71) 1) [ // do player have at least one lockpick
				lockpicking
			] [
				echo "You need a lockpick to open this chest"
			]
		] [
			echo "You need better skills to open this chest"
		]
	] [
		echo "this chest is empty"
	]
]
newgui lootchest01 [
	guistayopen [
		guitext "25 silver coins" 0
		guitext "" 0
		guibutton "Take all" [cleargui] 0
	]
] 0

// a simple "hacking" script

digit_1 = 1
digit_1_set = 0
digit_2 = 2
digit_2_set = 0
digit_3 = 3
digit_3_set = 0

alias hackerror [
	digit_1_set = 0
	digit_2_set = 0
	digit_3_set = 0
	sound 74
	cleargui
]

newgui hack [
	guistayopen [
		guitext "Please enter your" 0
		guitext "four digit pincode" 0
		guibar
		guilist [
			guibutton "" [ hackerror ] "7"
			guibutton "" [ hackerror ] "8"
			guibutton "" [ hackerror ] "9"
		]
		guilist [
			guibutton "" [
				if (= $digit_3_set 1) [
					sound 75
					hackcomplete = 1
					echo "access granted"
					trigger 2 1
					sound 76
					cleargui
				] [	hackerror ]
			] "4"
			guibutton "" [ hackerror ] "5"
			guibutton "" [ hackerror ] "6"
		]
		guilist [
			guibutton "" [
				if (= $digit_1_set 0) [
					digit_1_set = 1
					sound 75
				] [	hackerror ]
			] "1"
			guibutton "" [
				if ((= $digit_1_set 1) && (= $digit_2_set 0)) [
					digit_2_set = 1
					sound 75
				] [	hackerror ]
			] "2"
			guibutton "" [
				if ((= $digit_2_set 1) && (= $digit_3_set 0)) [
					digit_3_set = 1
					sound 75
				] [	hackerror ]
			] "3"
		]
		guilist [
			guibutton "" [echo "Nothing here"] "64x64"
			guibutton "" [ hackerror ] "\0"
			guibutton "" [echo "Nothing here"] "64x64"
		]
		guibar
		guibutton "Abort" [
			digit_1_set = 0
			digit_2_set = 0
			digit_3_set = 0
			cleargui
		]
	]
]